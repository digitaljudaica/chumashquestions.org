plugins {
  id 'com.github.ben-manes.versions' version '0.44.0'
  id 'org.asciidoctor.jvm.convert'   version '3.3.2'
  id 'org.asciidoctor.jvm.gems'      version '3.3.2'
  id 'org.asciidoctor.jvm.pdf'       version '3.3.2'
}

repositories {
  mavenCentral()
  ruby.gems()
}

dependencies {
  asciidoctorGems 'rubygems:asciidoctor-multipage:0.0.16'
}

final File asciidocSourceDir = file("$projectDir/src/docs/asciidoc/")
final File asciidocOutputDir = file("$buildDir/docs/asciidoc")
final File siteDir = file("$projectDir/docs")

asciidoctor {
  dependsOn(asciidoctorGemsPrepare)

  sourceDir asciidocSourceDir
  inputs.dir(asciidocSourceDir)
  sources {
    include 'book.adoc'
    include 'index.adoc'
  }

  outputDir asciidocOutputDir
  outputs.dir(asciidocOutputDir)
  outputOptions {
    backends = ['pdf', 'html5', 'multipage_html5']
  }

  doLast {
    project.copy {
      into siteDir

      from "$asciidocOutputDir/html5/styles.css"
      from "$asciidocOutputDir/html5/index.html"
      from "$asciidocOutputDir/html5/book.html"
      from "$asciidocOutputDir/pdf/book.pdf"
    }
    project.copy {
      into "$siteDir/html"
      from "$asciidocOutputDir/multipage_html5"
      exclude 'index.html'
    }
  }

  asciidoctorj {
    modules.pdf.version '2.3.4'
    requires 'asciidoctor-multipage'
  }

  attributes = [
    'allow-uri-read': true // API/CLI only
  ]
}

build.dependsOn(asciidoctor)

final String httpPort = '8080'
tasks.register('serveSite', Exec) {
  group = 'documentation'
  dependsOn(asciidoctor)
  doFirst {
    logger.lifecycle("Serving " + siteDir + " on port " + httpPort)
  }
  workingDir siteDir
  commandLine 'python', '-m', 'http.server', httpPort
}
