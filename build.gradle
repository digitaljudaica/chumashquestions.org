plugins {
  id 'com.github.ben-manes.versions' version '0.44.0'
  id 'org.asciidoctor.jvm.convert'   version '3.3.2'
  id 'org.asciidoctor.jvm.gems'      version '3.3.2'
  id 'org.asciidoctor.jvm.pdf'       version '3.3.2'
}

repositories {
  mavenCentral()
  ruby.gems()
}

dependencies {
  asciidoctorGems 'rubygems:asciidoctor-multipage:0.0.16'
}

final File asciidocSourceDir = file("$projectDir/src/docs/asciidoc/")
final File asciidocOutputDir = file("$buildDir/docs/asciidoc")
final File siteDir = file("$projectDir/docs")

asciidoctor {
  dependsOn(asciidoctorGemsPrepare)

  sourceDir asciidocSourceDir
  inputs.dir(asciidocSourceDir)

  sources {
    include 'book.adoc'
    include 'index.adoc'
  }

  outputDir asciidocOutputDir

  outputOptions {
    backends = [
      'pdf',
      'html5',
      'multipage_html5',
    ]
  }

  doFirst {
    project.delete(asciidocOutputDir)
  }

  doLast {
    project.sync {
      from "$asciidocOutputDir/multipage_html5"
      into "$siteDir/html"
    }
    project.sync {
      from "$asciidocOutputDir/html5"
      into "$siteDir/html-one"
    }
    project.sync {
      from "$asciidocOutputDir/pdf"
      into "$siteDir/pdf"
    }
    project.copy {
      from "$asciidocOutputDir/html5/index.html"
      into siteDir
    }
  }

  attributes = [
    reproducible: true,
    revdate: '2023-04-09',
    title: 'The Chumash Questions: A workbook for the weekly Torah portion based on the commentary of Rashi; includes holiday questions. 2nd edition.',
    author: 'Rabbi Dovid Wichnin',
    favicon: '/images/favicon-author.png',
    docinfo: 'shared',
    docinfodir: "$asciidocSourceDir/docinfo/",
    'allow-uri-read': true,
    sectlinks: true,
    sectanchors: true,
    'hide-uri-scheme': true
  ]

  asciidoctorj {
    requires 'asciidoctor-multipage'
    modules.pdf.version '2.3.4'
  }
}

build.dependsOn(asciidoctor)

final String httpPort = '8080'
tasks.register('serveSite', Exec) {
  group = 'documentation'

  dependsOn(asciidoctor)

  doFirst {
    logger.lifecycle("Serving " + siteDir + " on port " + httpPort)
  }

  workingDir siteDir
  commandLine 'python', '-m', 'http.server', httpPort
}
